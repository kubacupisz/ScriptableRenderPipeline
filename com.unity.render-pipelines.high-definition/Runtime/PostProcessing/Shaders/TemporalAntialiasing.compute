#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Color.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/Material/Builtin/BuiltinData.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/PostProcessing/Shaders/TemporalAntialiasing.hlsl"

#pragma kernel KTAA_Persp     MAIN=KTAA_Persp
#pragma kernel KTAA_Ortho     MAIN=KTAA_Ortho       ORTHOGRAPHIC
#pragma kernel KCopyHistory


TEXTURE2D_X(_InputTexture);
TEXTURE2D_X(_InputHistoryTexture);

RW_TEXTURE2D_X(float4, _OutputHistoryTexture);
RW_TEXTURE2D_X(float4, _OutputTexture);

#define GROUP_SIZE 8

#ifdef SHADER_API_XBOXONE
#define GROUP_SIZE 16
#endif

[numthreads(GROUP_SIZE, GROUP_SIZE,1)]
void MAIN(uint3 dispatchThreadId : SV_DispatchThreadID)
{
    UNITY_STEREO_ASSIGN_COMPUTE_EYE_INDEX(dispatchThreadId.z);
    PositionInputs posInputs = GetPositionInput(float2(dispatchThreadId.xy), _ScreenSize.zw, uint2(GROUP_SIZE, GROUP_SIZE));
    
    float2 jitter = _TaaJitterStrength.zw;

    #if defined(ORTHOGRAPHIC)
    // Don't dilate in ortho
    float2 closest = posInputs.positionSS;
    #else
    float2 closest = GetClosestFragment(posInputs);
    #endif

    float2 motionVector;
    DecodeMotionVector(LOAD_TEXTURE2D_X(_CameraMotionVectorsTexture, closest), motionVector);
    float motionVecLength = length(motionVector);

    float2 uv = posInputs.positionNDC - jitter;

//custom-begin: less destructive taa
#define DEMO 1
#define DEMO_FETCH Fetch// FetchBicubic

#if DEMO

    float3 history = Map(DEMO_FETCH(_InputHistoryTexture, posInputs.positionNDC - motionVector, 0.0, _ScreenToTargetScaleHistory.xy));
    float3 color = Map(DEMO_FETCH(_InputTexture, uv, 0.0, _ScreenToTargetScale.xy));
    float colorLuma = Luminance(color);

    // 3x3 neighbourhood
    #define SAMPLE_NEIGHBOUR(i,j) DEMO_FETCH(_InputTexture, uv, float2(j-1, i-1), _ScreenToTargetScale.xy)
    float3 _00 = SAMPLE_NEIGHBOUR(0, 0).rgb;
    float3 _01 = SAMPLE_NEIGHBOUR(0, 1).rgb;
    float3 _02 = SAMPLE_NEIGHBOUR(0, 2).rgb;
    float3 _10 = SAMPLE_NEIGHBOUR(1, 0).rgb;
    float3 _11 = SAMPLE_NEIGHBOUR(1, 1).rgb;
    float3 _12 = SAMPLE_NEIGHBOUR(1, 2).rgb;
    float3 _20 = SAMPLE_NEIGHBOUR(2, 0).rgb;
    float3 _21 = SAMPLE_NEIGHBOUR(2, 1).rgb;
    float3 _22 = SAMPLE_NEIGHBOUR(2, 2).rgb;

    // 3x3 minmax
    #ifndef Min3
    #define Min3(a,b,c) min(a,min(b,c))
    #endif
    #ifndef Max3
    #define Max3(a,b,c) max(a,max(b,c))
    #endif
    #define Min9(a,b,c,d,e,f,g,h,i) Min3(Min3(a,b,c),Min3(d,e,f),Min3(g,h,i))
    #define Max9(a,b,c,d,e,f,g,h,i) Max3(Max3(a,b,c),Max3(d,e,f),Max3(g,h,i))
    float3 minimum = Map(Min9(_00, _01, _02, _10, _11, _12, _20, _21, _22));
    float3 maximum = Map(Max9(_00, _01, _02, _10, _11, _12, _20, _21, _22));

#else
//custom-end:

    float3 color = Fetch(_InputTexture, uv, 0.0, _ScreenToTargetScale.xy);
    float3 history = Fetch(_InputHistoryTexture, posInputs.positionNDC - motionVector, 0.0, _ScreenToTargetScaleHistory.xy);

    float3 topLeft = Fetch(_InputTexture, uv, -RADIUS, _ScreenToTargetScale.xy);
    float3 bottomRight = Fetch(_InputTexture, uv, RADIUS, _ScreenToTargetScale.xy);

    float3 corners = 4.0 * (topLeft + bottomRight) - 2.0 * color;

    // Sharpen output
    #if SHARPEN
    float3 topRight = Fetch(_InputTexture, uv, float2(RADIUS, -RADIUS), _ScreenToTargetScale.xy);
    float3 bottomLeft = Fetch(_InputTexture, uv, float2(-RADIUS, RADIUS), _ScreenToTargetScale.xy);
    float3 blur = (topLeft + topRight + bottomLeft + bottomRight) * 0.25;
    color += (color - blur) * SHARPEN_STRENGTH;
    #endif
    
    color = clamp(color, 0.0, CLAMP_MAX);

    float3 average = Map((corners + color) / 7.0);

    topLeft = Map(topLeft);
    bottomRight = Map(bottomRight);
    color = Map(color);

    float colorLuma = Luminance(color);
    float averageLuma = Luminance(average);
    float nudge = lerp(4.0, 0.25, saturate(motionVecLength * 100.0)) * abs(averageLuma - colorLuma);

    float3 minimum = min(bottomRight, topLeft) - nudge;
    float3 maximum = max(topLeft, bottomRight) + nudge;

    history = Map(history);

//custom-begin: less destructive taa
#endif
//custom-end:

    // Clip history samples
    #if CLIP_AABB
    history = ClipToAABB(history, minimum, maximum);
    #else
    history = clamp(history, minimum, maximum);
    #endif

    // Blend color & history
    // Feedback weight from unbiased luminance diff (Timothy Lottes)
    float historyLuma = Luminance(history);
    float diff = abs(colorLuma - historyLuma) / Max3(colorLuma, historyLuma, 0.2);
    float weight = 1.0 - diff;
    float feedback = lerp(FEEDBACK_MIN, FEEDBACK_MAX, weight * weight);

    color = Unmap(lerp(color, history, feedback));
    color = clamp(color, 0.0, CLAMP_MAX);
    
    _OutputTexture[COORD_TEXTURE2D_X(posInputs.positionSS)] = float4(color.xyz, 1.0);
    _OutputHistoryTexture[COORD_TEXTURE2D_X(posInputs.positionSS)] = float4(color.xyz, 1.0);
}

[numthreads(8,8,1)]
void KCopyHistory(uint3 dispatchThreadId : SV_DispatchThreadID)
{
    UNITY_STEREO_ASSIGN_COMPUTE_EYE_INDEX(dispatchThreadId.z);

    if (any(dispatchThreadId.xy > uint2(_ScreenSize.xy)))
        return;  // Out of bounds, discard

    _OutputTexture[COORD_TEXTURE2D_X(dispatchThreadId.xy)] = _InputTexture[COORD_TEXTURE2D_X(dispatchThreadId.xy)];
}
